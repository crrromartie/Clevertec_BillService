plugins {
    id 'application'
    id 'war'
    id 'com.github.johnrengelman.shadow' version '6.1.0'
    id 'ru.clevertec.downloadPlugin' version '1.0'
}

configurations {
    ajc
}

application {
    mainClassName = 'ru.clevertec.bill.runner.Runner'
}

repositories {
    mavenLocal()
    mavenCentral()
    flatDir {
        dirs 'libs'
    }
}

dependencies {
    implementation 'jstl:jstl:1.2'
    implementation group: 'org.apache.logging.log4j', name: 'log4j-api', version: '2.14.0'
    implementation group: 'org.apache.logging.log4j', name: 'log4j-core', version: '2.14.0'
    implementation 'com.itextpdf:itextpdf:5.5.13.2'

    compile group: 'org.postgresql', name: 'postgresql', version: '42.2.18'
    compile group: 'com.google.code.gson', name: 'gson', version: '2.7'
    compile group: 'com.sun.mail', name: 'javax.mail', version: '1.6.2'
    compile group: 'org.aspectj', name: 'aspectjrt', version: '1.9.6'
    compile group: 'org.aspectj', name: 'aspectjweaver', version: '1.9.6'
    compile group: 'ru.clevertec', name: 'Clevertec_CustomThreadSaveCollection', version: '1.0'
    compile group: 'ru.clevertec', name: 'Clevertec_CustomJsonParser', version: '1.0'
    compile group: 'ru.clevertec', name: 'Clevertec_Logger_annotation', version: '1.0'

    compileOnly 'org.apache.tomcat:tomcat-servlet-api:9.0.37'
    compileOnly 'javax.servlet:javax.servlet-api:4.0.1'

    ajc 'org.aspectj:aspectjtools:1.9.6'

    testImplementation 'org.junit.jupiter:junit-jupiter-api:5.3.1'
    testRuntimeOnly 'org.junit.jupiter:junit-jupiter-engine:5.3.1'
}

group = 'ru.clevertec'
version = '1.0'
description = 'Clevertec_BillService'
java.sourceCompatibility = JavaVersion.VERSION_14

sourceSets {
    aspects {
        java {
            srcDir 'src/aspects/aspectj'
            include '**/*.java'
            compileClasspath = sourceSets.main.compileClasspath
            runtimeClasspath = sourceSets.main.runtimeClasspath
        }
    }
}

test {
    useJUnitPlatform()
}

jar {
    manifest {
        attributes 'Main-Class': 'ru.clevertec.bill.runner.Runner',
                "Multi-Release": true
    }
}

def aspectj = { sourceFileSet, destDir ->
    ant.taskdef(
            resource: "org/aspectj/tools/ant/taskdefs/aspectjTaskdefs.properties",
            classpath: configurations.ajc.asPath
    )

    ant.iajc(maxmem: "1024m",
            fork: "true",
            Xlint: "warning",
            classpath: sourceFileSet.runtimeClasspath.asPath,
            destDir: destDir,
            source: project.sourceCompatibility,
            target: project.targetCompatibility) {
        sourceroots {
            sourceFileSet.java.srcDirs.each { dir ->
                if (file(dir).exists()) {
                    pathelement(path: dir)
                }
            }
        }
    }
}

compileJava {
    doLast {
        aspectj(project.sourceSets.aspects, "$buildDir/classes/java/main")
    }
}

source.templateUrl = 'https://github.com/stebadmitriy/files/raw/main/Clevertec_Template.pdf'
source.targetFile = 'C:/Users/IGOR/IdeaProjects/Clevertec_BillService/src/main/resources/templates/Clevertec_Template.pdf'
build.dependsOn downloadPlugin
